; Modified version of https://github.com/GitJer/Some_RPI-Pico_stuff/blob/main/Rotary_encoder/pio_rotary_encoder.pio
; uses 26/32 instructions
.program pio_encoder
.wrap_target
.origin 0        ; The jump table has to start at 0
                 ; it contains the correct jumps for each of the 16  
                 ; combination of 4 bits formed by A'B'AB
                 ; A = current reading of pin_A of the rotary encoder
                 ; A' = previous reading of pin_A of the rotary encoder
                 ; B = current reading of pin_B of the rotary encoder
                 ; B' = previous reading of pin_B of the rotary encoder
    jmp read     ; 0000 = from 00 to 00 = no change in reading
    jmp CW       ; 0001 = from 00 to 01 = clockwise rotation
    jmp CCW      ; 0010 = from 00 to 10 = counter clockwise rotation
    jmp read     ; 0011 = from 00 to 11 = error

    jmp CCW      ; 0100 = from 01 to 00 = counter clockwise rotation
    jmp read     ; 0101 = from 01 to 01 = no change in reading 
    jmp read     ; 0110 = from 01 to 10 = error
    jmp CW       ; 0111 = from 01 to 11 = clockwise rotation
 
    jmp CW       ; 1000 = from 10 to 00 = clockwise rotation
    jmp read     ; 1001 = from 10 to 01 = error
    jmp read     ; 1010 = from 10 to 10 = no change in reading 
    jmp CCW      ; 1011 = from 10 to 11 = counter clockwise rotation
 
    jmp read     ; 1100 = from 11 to 00 = error
    jmp CCW      ; 1101 = from 11 to 01 = counter clockwise rotation
    jmp CW       ; 1110 = from 11 to 10 = clockwise rotation
    jmp read     ; 1111 = from 11 to 11 = no change in reading 

pc_start:        ; this is the entry point for the program
    in pins 2    ; read the current values of A and B and use 
                 ; them to initialize the previous values (A'B')
read:
    mov y ISR    ; Temp storage for pushing to RX FIFO     
    mov ISR x    ; FIFO abuse, keep pushing x to FIFO, even if it is full
    push noblock ; To read, program will clear FIFO and wait for it to refill

    pull noblock ; Pulls a value from TX and stores it to OSR and copy it to scratch x
    mov x OSR    ; If there is nothing in TX, pull noblock is equivalent to mov OSR, x
                 ; Thus, this will only update x if the program pushes a value to it
    mov ISR y    ; Reload ISR from temp storage
    mov OSR ISR  ; the OSR is (after the next instruction) used to shift 
                 ; the two bits with the previous values into the ISR
    out ISR 2    ; shift the previous value into the ISR. This also sets
                 ; all other bits in the ISR to 0
    in pins 2    ; shift the current value into the ISR
                 ; the 16 LSB of the ISR now contain 000000000000A'B'AB
                 ; this represents a jmp instruction to the address A'B'AB 
    mov exec ISR ; do the jmp encoded in the ISR
CW:              ; a clockwise rotation was detected
    jmp x--, NOP1; Use JMP condition to decrement click count
NOP1:
    jmp read     ; jump to reading the current values of A and B
CCW:             ; a counter clockwise rotation was detected
    mov x, !x    ; use 1's complement identity that (x'-1)' = x+1
    jmp x--, NOP2
NOP2:
    mov x, !x
;    jmp read    ; jump to reading the current values of A and B.
                 ; the jmp isn't needed because of the .wrap, and the first 
                 ; statement of the program happens to be a jmp read
.wrap

% c-sdk {
static inline void encoder_program_init(PIO pio, uint sm, uint offset, uint low_pin) {
    pio_sm_config c = pio_encoder_program_get_default_config(offset);

    sm_config_set_in_pins(&c, low_pin);
    sm_config_set_in_shift(&c, false, false, 0);
    pio_sm_init(pio, sm, offset, &c);
    pio_sm_set_enabled(pio, sm, true);
}
%}